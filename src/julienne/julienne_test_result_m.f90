! Copyright (c) 2024-2025, The Regents of the University of California and Sourcery Institute
! Terms of use are as specified in LICENSE.txt
module julienne_test_result_m
  !! Define an abstraction for describing test intentions and results
  use julienne_string_m, only : string_t
  use julienne_test_diagnosis_m, only : test_diagnosis_t
  use julienne_user_defined_collectives_m, only : co_all
  implicit none

  private
  public :: test_result_t

  type test_result_t
    !! Encapsulate test descriptions and outcomes
    private
    type(string_t) :: description_
    type(test_diagnosis_t), allocatable :: diagnosis_
  contains
    procedure :: characterize
    procedure :: passed
    procedure :: skipped
    generic :: description_contains => description_contains_string, description_contains_characters
    procedure, private ::              description_contains_string, description_contains_characters
  end type

  interface test_result_t

    elemental module function construct_from_string(description, diagnosis) result(test_result)
      !! The result is a test_result_t object with the components defined by the dummy arguments
      implicit none
      type(string_t), intent(in) :: description
      type(test_diagnosis_t), intent(in), optional :: diagnosis
      type(test_result_t) test_result 
    end function

    elemental module function construct_from_character(description, diagnosis) result(test_result)
      !! The result is a test_result_t object with the components defined by the dummy arguments
      implicit none
      character(len=*), intent(in) :: description
      type(test_diagnosis_t), intent(in), optional :: diagnosis
      type(test_result_t) test_result 
    end function

  end interface

  interface

    pure module function characterize(self) result(characterization)
      !! The result is a character description of the test and its outcome
      implicit none
      class(test_result_t), intent(in) :: self
      character(len=:), allocatable :: characterization
    end function

    impure elemental module function passed(self) result(test_passed)
      !! The result is true if and only if the test passed on all images
      implicit none
      class(test_result_t), intent(in) :: self
      logical test_passed
    end function

    impure elemental module function skipped(self) result(test_skipped)
      !! The result is true if and only if the test result contains no diagnosis on any image
      implicit none
      class(test_result_t), intent(in) :: self
      logical test_skipped
    end function

    elemental module function description_contains_string(self, substring) result(substring_found)
      !! The result is true if and only if the test description contains the substring
      implicit none
      class(test_result_t), intent(in) :: self
      type(string_t), intent(in) :: substring
      logical substring_found
    end function

    elemental module function description_contains_characters(self, substring) result(substring_found)
      !! The result is true if and only if the test description contains the substring
      implicit none
      class(test_result_t), intent(in) :: self
      character(len=*), intent(in) :: substring
      logical substring_found
    end function

  end interface

contains

    module procedure construct_from_string
      test_result%description_ = description
      if (present(diagnosis)) test_result%diagnosis_ = diagnosis
    end procedure

    module procedure construct_from_character
      test_result%description_ = description
      if (present(diagnosis)) test_result%diagnosis_ = diagnosis
    end procedure

    module procedure characterize
      if (.not. allocated(self%diagnosis_)) then
        characterization = "SKIPS  on " // trim(self%description_%string()) // "."
      else
        associate(test_passed => self%diagnosis_%test_passed())
          characterization = merge("passes on ", "FAILS  on ", test_passed) // trim(self%description_%string()) // "."
          if (.not. test_passed) &
            characterization = characterization // new_line('') // "      diagnostics: " // self%diagnosis_%diagnostics_string()
        end associate
      end if
    end procedure

    module procedure passed
      if (.not. allocated(self%diagnosis_)) then
        test_passed = .false.
      else
        test_passed = self%diagnosis_%test_passed()
      end if
      call co_all(test_passed)
    end procedure

    module procedure skipped
      test_skipped = merge(.false., .true., allocated(self%diagnosis_))
      call co_all(test_skipped)
    end procedure

    module procedure description_contains_string
      substring_found = self%description_contains_characters(substring%string())
    end procedure

    module procedure description_contains_characters
      substring_found = index(self%description_%string(), substring) /= 0
    end procedure

end module julienne_test_result_m
